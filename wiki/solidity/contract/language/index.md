---
title: è¯­æ³•ç»“æ„
sidebar_label: ğŸ˜…è¯­æ³•ç»“æ„
sidebar_position: 20
image: sss
description: aaa
---
  ç”µè„‘ç«¯å¯ä»¥ç›´æ¥ä½¿ç”¨å³ä¾§ç›®å½•é€‰æ‹©æƒ³çœ‹çš„å†…å®¹ï¼Œæ‰‹æœºç«¯ä¸‹æ‹‰é€‰æ‹©å†…å®¹

### Hello-World

éƒ¨ç½²å³å¯æ‰“å°å‡ºHello Worldçš„æ—¥å¿—å†…å®¹ï¼ˆeventçš„ç”¨æ³•ä¸‹é¢ä¼šè¯´ï¼‰

  ```js
    // SPDX-License-Identifier: MIT
    // compiler version éœ€è¦å¤§äº0.8.13
    pragma solidity ^0.8.13;

    contract HelloWorld {
      event Print(string hi);

      constructor(){
          emit Print("Hello World!");
      }
    }
  ```

### æ•°æ®ç±»å‹Data-Types

  æˆ‘ä»¬å‘æ‚¨ä»‹ç»ä¸€äº›åœ¨Solidityä¸­å¯ç”¨çš„åŸå§‹æ•°æ®ç±»å‹ã€‚
  - boolean | uint | int | address | string | byte


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Primitives {
        bool public boo = true;

        /*
        Uintä»£è¡¨æ— ç¬¦å·æ•´æ•°ï¼Œå³éè´Ÿæ•´æ•°
        different sizes are available
            uint8   ranges from 0 to 2 ** 8 - 1
            uint16  ranges from 0 to 2 ** 16 - 1
            ... ä¾æ¬¡ç±»æ¨
            uint256 ranges from 0 to 2 ** 256 - 1
        */
        uint8 public u8 = 1;
        uint public u256 = 456;
        uint public u = 123; // uint is an alias for uint256

        /*
        æ•´æ•°ç±»å‹å…è®¸ä¸ºè´Ÿæ•°ã€‚
        ä¸uintä¸€æ ·ï¼Œä»int8åˆ°int256å¯ä»¥ä½¿ç”¨ä¸åŒçš„èŒƒå›´

        int256 ranges from -2 ** 255 to 2 ** 255 - 1
        int128 ranges from -2 ** 127 to 2 ** 127 - 1
        */
        int8 public i8 = -1;
        int public i256 = 456;
        int public i = -123; // int is same as int256

        // intçš„æœ€å°å€¼å’Œæœ€å¤§å€¼
        int public minInt = type(int).min;
        int public maxInt = type(int).max;

        address public addr = 0x39E4572A5e74Ff43DB3508f21F4Bfacd09408f62;

        /*
        åœ¨Solidityä¸­ï¼Œæ•°æ®ç±»å‹byteè¡¨ç¤ºä¸€ä¸ªå­—èŠ‚åºåˆ—ã€‚
        Solidity ä¸¤ç§bytesç±»å‹ :

        - å›ºå®šå¤§å°çš„
        - åŠ¨æ€å¤§å°
        
        Solidityä¸­çš„å­—èŠ‚è¡¨ç¤ºä¸€ä¸ªåŠ¨æ€bytesæ•°ç»„ã€‚
        å¿«é€Ÿçš„è¡¨è¾¾ byte[] .
        */
        bytes1 a = 0xb5; //  [10110101]
        bytes1 b = 0x56; //  [01010110]

        // ç±»å‹çš„é»˜è®¤å€¼
        // æœªèµ‹å€¼çš„å˜é‡æœ‰ä¸€ä¸ªé»˜è®¤å€¼
        bool public defaultBoo; // false
        uint public defaultUint; // 0
        int public defaultInt; // 0
        address public defaultAddr; // 0x0000000000000000000000000000000000000000
    }

  ```

### å˜é‡Variables
  
  æœ‰ä¸‰ç§ç±»å‹çš„å˜é‡ï¼Œåˆ†åˆ«ä¸ºçŠ¶æ€å˜é‡ã€å±€éƒ¨å˜é‡ã€å…¨å±€å˜é‡

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Variables {
        // çŠ¶æ€å˜é‡å­˜å‚¨åœ¨åŒºå—é“¾ä¸Šï¼ˆçŠ¶æ€å˜é‡ï¼‰
        string public text = "Hello";
        uint public num = 123;

        function doSomething() public {
            // å±€éƒ¨å˜é‡ä¸ä¿å­˜åˆ°åŒºå—é“¾ï¼ˆå±€éƒ¨å˜é‡ï¼‰
            uint i = 456;

            // Here are some global variables
            uint timestamp = block.timestamp; // å½“å‰åŒºå—çš„æ—¶é—´æˆ³ï¼ˆå…¨å±€å˜é‡ï¼‰
            address sender = msg.sender; // åœ°å€çš„è°ƒç”¨è€…ï¼ˆå…¨å±€å˜é‡ï¼‰
        }
    }
  ```

### å¸¸é‡Constants

  å¸¸é‡æ˜¯ä¸èƒ½ä¿®æ”¹çš„å˜é‡ã€‚å®ƒä»¬çš„å€¼æ˜¯ç¡¬ç¼–ç çš„ï¼Œä½¿ç”¨å¸¸é‡å¯ä»¥èŠ‚çœgasæˆæœ¬ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Constants {
        // å¸¸é‡å˜é‡ä¸€èˆ¬éƒ½æ˜¯å¤§å†™è¡¨ç¤º
        address public constant MY_ADDRESS = 0x39E4572A5e74Ff43DB3508f21F4Bfacd09408f62;
        uint public constant MY_UINT = 1111;
    }

  ```

### ä¸å¯å˜Immutable

  ä¸å¯å˜å˜é‡å°±åƒå¸¸é‡ã€‚ä¸å¯å˜å˜é‡çš„å€¼å¯ä»¥åœ¨æ„é€ å‡½æ•°å†…éƒ¨è®¾ç½®ï¼Œä½†ä¸èƒ½åœ¨ä¹‹åä¿®æ”¹ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Immutable {
        // coding convention to uppercase constant variables
        address public immutable MY_ADDRESS;
        uint public immutable MY_UINT;

        constructor(uint _myUint) {
            MY_ADDRESS = msg.sender;
            MY_UINT = _myUint;
        }
    }

  ```

### è¯»å–å†™å…¥å˜é‡Reading-and-Writing-to-a-State-Variable

  è¦ç¼–å†™æˆ–æ›´æ–°çŠ¶æ€å˜é‡ï¼Œæ‚¨éœ€è¦å‘é€ä¸€ä¸ªäº‹åŠ¡ã€‚å¦ä¸€æ–¹é¢ï¼Œä½ å¯ä»¥å…è´¹è¯»å–çŠ¶æ€å˜é‡ï¼Œä¸éœ€è¦ä»»ä½•äº¤æ˜“è´¹ç”¨ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract SimpleStorage {
        // çŠ¶æ€å˜é‡å­˜å‚¨
        uint public num;

        // æ‚¨éœ€è¦å‘é€ä¸€ä¸ªäº‹åŠ¡æ¥å†™å…¥çŠ¶æ€å˜é‡ã€‚
        function set(uint _num) public {
            num = _num;
        }

        // æ‚¨å¯ä»¥åœ¨ä¸å‘é€äº‹åŠ¡çš„æƒ…å†µä¸‹è¯»å–çŠ¶æ€å˜é‡ã€‚
        function get() public view returns (uint) {
            return num;
        }
    }

  ```

### å•ä½ether-and-Wei

  ç”¨ ether æ”¯ä»˜äº¤æ˜“ã€‚
  å¦‚ï¼š1 ether ç­‰äº 10^18 weiã€‚
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract EtherUnits {
        uint public oneWei = 1 wei;
        // 1 wei = 1
        bool public isOneWei = 1 wei == 1;

        uint public oneEther = 1 ether;
        // 1 ether = 10^18 wei
        bool public isOneEther = 1 ether == 1e18;
    }

  ```

### Gas-and-GasPrice

  ä¸€ç¬”äº¤æ˜“ä¼šèŠ±è´¹å¤šå°‘ether

  è®¡ç®—å‡½æ•°ï¼š`gas spent * gas price`
  
  1. `gas`æ˜¯ä¸€ä¸ªè®¡ç®—å•ä½
  2. `gas spent`æ˜¯åœ¨æ­¤äº¤æ˜“ä¸‹ä¼šä½¿ç”¨çš„
  3. `gas price`æ˜¯ä½ æƒ³è¦æ”¯ä»˜çš„etherçš„å•ä»·


  åœ¨ä¸€ä¸ªå—ä¸­ï¼Œ`gas price`è¾ƒé«˜çš„ä¼šä¼˜å…ˆæ‰“åŒ…
  æ²¡æœ‰èŠ±è´¹å®Œçš„gasä¼šé€€è¿˜ï¼ˆå½“ç„¶è¿™ä¸ªè®°å½•æ˜¯çœ‹ä¸åˆ°çš„ï¼‰
  `Gas Limit`
  æ¶ˆè€—çš„gasä¸Šé™ï¼Œè¿™é‡Œæœ‰ä¸¤ä¸ªä¸Šé™ï¼š

  1. `gas limit`ä½ æƒ³è¦æ”¯ä»˜çš„æœ€å¤§çš„gasè´¹ä¸Šé™ï¼ˆå½“ç„¶è¦æ»¡è¶³å¯ä»¥æ”¯ä»˜çš„æ¡ä»¶æ‰ä¼šæˆåŠŸï¼‰
  2. `block gas limit`åŒºå—å…è®¸çš„æœ€å¤§gasä¸Šé™ï¼Œæ­¤ä¸ºåŒºå—é“¾å†³å®šçš„ï¼ˆå¯ä»¥é˜²æ­¢ddosæ”»å‡»ï¼Œé˜²æ­¢åˆçº¦è¿‡åˆ†æ¶ˆè€—èµ„æºï¼‰


  å¦‚ä¸‹é¢è¡¨è¿°ï¼š
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Gas {
        uint public i = 0;

        // è€—å°½æ‰€æœ‰å‘é€çš„gasä¼šå¯¼è‡´äº¤æ˜“å¤±è´¥ã€‚
        // çŠ¶æ€æ›´æ”¹è¢«æ’¤æ¶ˆã€‚
        // Gas spent ä¸ä¼šè¢«é€€æ¬¾
        function forever() public {
            // æ­¤å‡½æ•°ä¼šæ¶ˆè€—å½“å‰åŒºå—çš„æ‰€æœ‰gasï¼ŒåŠè¶…å‡ºåŒºå—çš„gas limitä¸Šé™ï¼Œä¼šå¤±è´¥
            while (true) {
                i += 1;
            }
        }
    }

  ```

### åˆ¤æ–­è¯­å¥If-Else
    
  Solidity æ”¯æŒæ¡ä»¶è¯­å¥ifã€else ifå’Œelseã€‚
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract IfElse {
        function foo(uint x) public pure returns (uint) {
            if (x < 10) {
                return 0;
            } else if (x < 20) {
                return 1;
            } else {
                return 2;
            }
        }

        function ternary(uint _x) public pure returns (uint) {
            // if (_x < 10) {
            //     return 1;
            // }
            // return 2;

            // åŒæ ·æ”¯æŒä¸‰ç›®è¿ç®—
            return _x < 10 ? 1 : 2;
        }
    }

  ```
### å¾ªç¯è¯­å¥For-and-While-Loop

  Solidityæ”¯æŒforã€whileå’Œdo whileå¾ªç¯ã€‚
  ä¸è¦ç¼–å†™æ— ç•Œå¾ªç¯ï¼Œå› ä¸ºè¿™å¯èƒ½è¶…è¿‡`gas limit`ï¼Œå¯¼è‡´äº‹åŠ¡å¤±è´¥ã€‚
  ç”±äºä¸Šè¿°åŸå› ï¼Œwhileå’Œdo whileå¾ªç¯å¾ˆå°‘ä½¿ç”¨ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Loop {
        function loop() public {
            // for loop
            for (uint i = 0; i < 10; i++) {
                if (i == 3) {
                    // Skip to next iteration with continue
                    continue;
                }
                if (i == 5) {
                    // Exit loop with break
                    break;
                }
            }

            // while loop
            uint j;
            while (j < 10) {
                j++;
            }
        }
    }

  ```
### æ˜ å°„Mapping

  æ˜ å°„æ˜¯ä½¿ç”¨è¯­æ³•æ˜ å°„ `mapping(keyType => valueType)`ã€‚
  keyTypeå¯ä»¥æ˜¯ä»»ä½•å†…ç½®å€¼ç±»å‹ã€å­—èŠ‚ã€å­—ç¬¦ä¸²æˆ–ä»»ä½•åˆçº¦ã€‚
  valueTypeå¯ä»¥æ˜¯ä»»ä½•ç±»å‹ï¼ŒåŒ…æ‹¬å¦ä¸€ä¸ªæ˜ å°„æˆ–æ•°ç»„ã€‚
  Mappings æ²¡æœ‰è¿­ä»£å™¨

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Mapping {
        // address åˆ° uint çš„æ˜ å°„
        mapping(address => uint) public myMap;

        function get(address _addr) public view returns (uint) {
            // å§‹ç»ˆè¿”å›ä¸€ä¸ªå€¼çš„æ˜ å°„ã€‚
            // å¦‚æœä»æœªè®¾ç½®è¯¥å€¼ï¼Œå®ƒå°†è¿”å›é»˜è®¤å€¼ã€‚
            return myMap[_addr];
        }

        function set(address _addr, uint _i) public {
            // æ›´æ–°è¿™ä¸ª_addrçš„uintå€¼
            myMap[_addr] = _i;
        }

        function remove(address _addr) public {
            // é‡æ–°è®¾ç½®ä¸ºé»˜è®¤å€¼ã€‚
            delete myMap[_addr];
        }
    }

    contract NestedMapping {
        // åµŒå¥—çš„ mapping
        mapping(address => mapping(uint => bool)) public nested;

        function get(address _addr1, uint _i) public view returns (bool) {
            // å¯ä»¥ä»åµŒå¥—æ˜ å°„ä¸­è·å–å€¼
            // æ²¡æœ‰è®¾ç½®å€¼ä¹Ÿå¯ä»¥è·å–é»˜è®¤å€¼
            return nested[_addr1][_i];
        }

        function set(
            address _addr1,
            uint _i,
            bool _boo
        ) public {
            nested[_addr1][_i] = _boo;
        }

        function remove(address _addr1, uint _i) public {
            delete nested[_addr1][_i];
        }
    }

  ```

### æ•°ç»„Array

  æ•°ç»„å¯ä»¥å…·æœ‰ç¼–è¯‘æ—¶å›ºå®šçš„å¤§å°ï¼Œä¹Ÿå¯ä»¥å…·æœ‰åŠ¨æ€çš„å¤§å°ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Array {
        // Several ways to initialize an array
        uint[] public arr;
        uint[] public arr2 = [1, 2, 3];
        // Fixed sized array, all elements initialize to 0
        uint[10] public myFixedSizeArr;

        function get(uint i) public view returns (uint) {
            return arr[i];
        }

        // Solidity can return the entire array.
        // But this function should be avoided for
        // arrays that can grow indefinitely in length.
        function getArr() public view returns (uint[] memory) {
            return arr;
        }

        function push(uint i) public {
            // Append to array
            // This will increase the array length by 1.
            arr.push(i);
        }

        function pop() public {
            // Remove last element from array
            // This will decrease the array length by 1
            arr.pop();
        }

        function getLength() public view returns (uint) {
            return arr.length;
        }

        function remove(uint index) public {
            // Delete does not change the array length.
            // It resets the value at index to it's default value,
            // in this case 0
            delete arr[index];
        }

        function examples() external {
            // create array in memory, only fixed size can be created
            uint[] memory a = new uint[](5);
        }
    }

  ```

  åˆ é™¤æ•°ç»„å…ƒç´ çš„ä¾‹å­
  é€šè¿‡ä»å³åˆ°å·¦ç§»åŠ¨å…ƒç´ æ¥ç§»é™¤æ•°ç»„å…ƒç´ 

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract ArrayRemoveByShifting {
      // [1, 2, 3] -- remove(1) --> [1, 3, 3] --> [1, 3]
      // [1, 2, 3, 4, 5, 6] -- remove(2) --> [1, 2, 4, 5, 6, 6] --> [1, 2, 4, 5, 6]
      // [1, 2, 3, 4, 5, 6] -- remove(0) --> [2, 3, 4, 5, 6, 6] --> [2, 3, 4, 5, 6]
      // [1] -- remove(0) --> [1] --> []

      uint[] public arr;

      function remove(uint _index) public {
          require(_index < arr.length, "index out of bound");

          for (uint i = _index; i < arr.length - 1; i++) {
              arr[i] = arr[i + 1];
          }
          arr.pop();
      }

      function test() external {
          arr = [1, 2, 3, 4, 5];
          remove(2);
          // [1, 2, 4, 5]
          assert(arr[0] == 1);
          assert(arr[1] == 2);
          assert(arr[2] == 4);
          assert(arr[3] == 5);
          assert(arr.length == 4);

          arr = [1];
          remove(0);
          // []
          assert(arr.length == 0);
      }
    }


  ```

  é€šè¿‡å°†æœ€åä¸€ä¸ªå…ƒç´ å¤åˆ¶åˆ°è¦åˆ é™¤çš„ä½ç½®æ¥åˆ é™¤æ•°ç»„å…ƒç´ 

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract ArrayReplaceFromEnd {
      uint[] public arr;

      // Deleting an element creates a gap in the array.
      // One trick to keep the array compact is to
      // move the last element into the place to delete.
      function remove(uint index) public {
          // Move the last element into the place to delete
          arr[index] = arr[arr.length - 1];
          // Remove the last element
          arr.pop();
      }

      function test() public {
          arr = [1, 2, 3, 4];

          remove(1);
          // [1, 4, 3]
          assert(arr.length == 3);
          assert(arr[0] == 1);
          assert(arr[1] == 4);
          assert(arr[2] == 3);

          remove(2);
          // [1, 4]
          assert(arr.length == 2);
          assert(arr[0] == 1);
          assert(arr[1] == 4);
      }
    }


  ```

### æšä¸¾Enum

  solidityæ”¯æŒå¯æšä¸¾å¯¹è±¡ï¼Œå®ƒä»¬å¯¹å»ºæ¨¡é€‰æ‹©å’Œè·Ÿè¸ªçŠ¶æ€å¾ˆæœ‰ç”¨ã€‚æšä¸¾å¯ä»¥åœ¨solidityä¹‹å¤–å£°æ˜ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Enum {
        // Enum representing shipping status
        enum Status {
            Pending,
            Shipped,
            Accepted,
            Rejected,
            Canceled
        }

        // Default value is the first element listed in
        // definition of the type, in this case "Pending"
        Status public status;

        // Returns uint
        // Pending  - 0
        // Shipped  - 1
        // Accepted - 2
        // Rejected - 3
        // Canceled - 4
        function get() public view returns (Status) {
            return status;
        }

        // Update status by passing uint into input
        function set(Status _status) public {
            status = _status;
        }

        // You can update to a specific enum like this
        function cancel() public {
            status = Status.Canceled;
        }

        // delete resets the enum to its first value, 0
        function reset() public {
            delete status;
        }
    }

  ```

  å£°æ˜å¹¶å¯¼å…¥Enumæ–‡ä»¶ï¼ŒEnumåœ¨å…¶ä¸­å£°æ˜

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;
    // This is saved 'EnumDeclaration.sol'

    enum Status {
        Pending,
        Shipped,
        Accepted,
        Rejected,
        Canceled
    }

  ```
  å¯¼å…¥ä¸Šè¿°æšä¸¾çš„æ–‡ä»¶

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    import "./EnumDeclaration.sol";

    contract Enum {
        Status public status;
    }

  ```

### ç»“æ„Structs

  æ‚¨å¯ä»¥é€šè¿‡åˆ›å»º`struct`æ¥å®šä¹‰è‡ªå·±çš„ç±»å‹ã€‚å®ƒä»¬å¯¹å°†ç›¸å…³æ•°æ®åˆ†ç»„å¾ˆæœ‰ç”¨ã€‚ç»“æ„å¯ä»¥åœ¨ä¸€ä¸ªå¥‘çº¦ä¹‹å¤–å£°æ˜å¹¶å¯¼å…¥åˆ°å¦ä¸€ä¸ªå¥‘çº¦ä¸­ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Todos {
        struct Todo {
            string text;
            bool completed;
        }

        // An array of 'Todo' structs
        Todo[] public todos;

        function create(string calldata _text) public {
            // 3 ways to initialize a struct
            // - calling it like a function
            todos.push(Todo(_text, false));

            // key value mapping
            todos.push(Todo({text: _text, completed: false}));

            // initialize an empty struct and then update it
            Todo memory todo;
            todo.text = _text;
            // todo.completed initialized to false

            todos.push(todo);
        }

        // Solidity automatically created a getter for 'todos' so
        // you don't actually need this function.
        function get(uint _index) public view returns (string memory text, bool completed) {
            Todo storage todo = todos[_index];
            return (todo.text, todo.completed);
        }

        // update text
        function updateText(uint _index, string calldata _text) public {
            Todo storage todo = todos[_index];
            todo.text = _text;
        }

        // update completed
        function toggleCompleted(uint _index) public {
            Todo storage todo = todos[_index];
            todo.completed = !todo.completed;
        }
    }

  ```

### æ•°æ®å£°æ˜Storage-Memory-and-Calldata

  å˜é‡å£°æ˜ä¸º`storage`ã€`memory`æˆ–`calldata`ï¼Œä»¥æ˜¾å¼æŒ‡å®šæ•°æ®çš„ä½ç½®ã€‚
1. `storage` - å˜é‡æ˜¯ä¸€ä¸ªçŠ¶æ€å˜é‡(å­˜å‚¨åœ¨åŒºå—é“¾ä¸Š)
2. `memory` - å˜é‡åœ¨å†…å­˜ä¸­ï¼Œå½“å‡½æ•°è¢«è°ƒç”¨æ—¶å®ƒå°±å­˜åœ¨
3. `calldata` - åŒ…å«å‡½æ•°å‚æ•°çš„ç‰¹æ®Šæ•°æ®ä½ç½®
   

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract DataLocations {
        uint[] public arr;
        mapping(uint => address) map;
        struct MyStruct {
            uint foo;
        }
        mapping(uint => MyStruct) myStructs;

        function f() public {
            // call _f with state variables
            _f(arr, map, myStructs[1]);

            // get a struct from a mapping
            MyStruct storage myStruct = myStructs[1];
            // create a struct in memory
            MyStruct memory myMemStruct = MyStruct(0);
        }

        function _f(
            uint[] storage _arr,
            mapping(uint => address) storage _map,
            MyStruct storage _myStruct
        ) internal {
            // do something with storage variables
        }

        // You can return memory variables
        function g(uint[] memory _arr) public returns (uint[] memory) {
            // do something with memory array
        }

        function h(uint[] calldata _arr) external {
            // do something with calldata array
        }
    }

  ```


### å‡½æ•°Function

  å‡½æ•°çš„è¿”å›å€¼æœ‰å¤šç§ç±»å‹
  å…¬å…±å‡½æ•°ä¸èƒ½æ¥å—æŸäº›æ•°æ®ç±»å‹ä½œä¸ºè¾“å…¥æˆ–è¾“å‡º
  é’ˆå¯¹äºå‡½æ•°ï¼Œç»™ä¸€ä¸ªç®€å•çš„ä»‹ç»

```js
    function FunctionName([parameters]) 
        {public|private|internal|external} 
        [pure|constant|view|payable]
        [modifiers]
        [returns(return types)]
```

ä¸€ä¸ªå‡½æ•°åˆ†ä¸ºå¤šä¸ªæ¨¡å—ï¼Œ
- `function`ä½œä¸ºå‡½æ•°å£°æ˜ï¼Œ
- `FunctionName`ä»£è¡¨å‡½æ•°åï¼Œ
- `{public|private|internal|external} `ä»£è¡¨å‡½æ•°çš„å¯è§æ€§[Visibility](#å¯è§æ€§visibility)ï¼Œ
- `[pure|view|payable|constant]`ä»£è¡¨å‡½æ•°è¿”å›ç±»å‹æˆ–æ‰§è¡Œç±»å‹ [view](#è§†å›¾view-and-pure-functions),å…¶ä¸­`payable`ä»£è¡¨å¯æ”¯ä»˜[Payable](#å¯æ”¯ä»˜payable)ï¼Œ`constant`ä»£è¡¨å¸¸é‡ä¸å˜é‡çš„å£°æ˜æ˜¯ä¸€æ ·çš„ï¼ˆç›®å‰å·²ä¸å†ä½¿ç”¨ï¼Œå¯ä»¥å¿½ç•¥ï¼Œä½¿ç”¨`pure`ã€`view`å³å¯ï¼‰
- `[modifiers]`ä»£è¡¨å‡½æ•°çš„ä¿®é¥°ï¼Œåœ¨å‡½æ•°é€»è¾‘ä»£ç ä¹‹å‰æˆ–ä¹‹åæ‰§è¡Œ[modifiers](#å‡½æ•°ä¿®é¥°function-modifier)

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Function {
        // Functions can return multiple values.
        function returnMany()
            public
            pure
            returns (
                uint,
                bool,
                uint
            )
        {
            return (1, true, 2);
        }

        // Return values can be named.
        function named()
            public
            pure
            returns (
                uint x,
                bool b,
                uint y
            )
        {
            return (1, true, 2);
        }

        // Return values can be assigned to their name.
        // In this case the return statement can be omitted.
        function assigned()
            public
            pure
            returns (
                uint x,
                bool b,
                uint y
            )
        {
            x = 1;
            b = true;
            y = 2;
        }

        // Use destructuring assignment when calling another
        // function that returns multiple values.
        function destructuringAssignments()
            public
            pure
            returns (
                uint,
                bool,
                uint,
                uint,
                uint
            )
        {
            (uint i, bool b, uint j) = returnMany();

            // Values can be left out.
            (uint x, , uint y) = (4, 5, 6);

            return (i, b, j, x, y);
        }

        // Cannot use map for either input or output

        // Can use array for input
        function arrayInput(uint[] memory _arr) public {}

        // Can use array for output
        uint[] public arr;

        function arrayOutput() public view returns (uint[] memory) {
            return arr;
        }
    }

  ```



### å¯è§æ€§Visibility

  å‡½æ•°å’ŒçŠ¶æ€å˜é‡å¿…é¡»å£°æ˜å®ƒä»¬æ˜¯å¦å¯ä»¥è¢«å…¶ä»–åˆçº¦è®¿é—®ã€‚

  å‡½æ•°å¯ä»¥å£°æ˜ä¸º
  - `public` - ä»»ä½•åˆçº¦å’Œå¸æˆ·éƒ½å¯ä»¥è°ƒç”¨
  - `private` - ä»…åœ¨å®šä¹‰åŠŸèƒ½çš„åˆçº¦å†…
  - `internal` - ä»…ç»§æ‰¿å†…éƒ¨åŠŸèƒ½çš„å†…éƒ¨åˆçº¦
  - `external` - åªæœ‰å…¶ä»–åˆçº¦å’Œå¸æˆ·æ‰èƒ½è°ƒç”¨ï¼Œå†…éƒ¨ä¸å¯è°ƒç”¨


  çŠ¶æ€å˜é‡å¯ä»¥è¢«å£°æ˜ä¸º`public`ï¼Œ`private`ï¼Œ`internal`ä½†æ˜¯ä¸èƒ½ä½¿ç”¨`external`

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Base {
        // Private function can only be called
        // - inside this contract
        // Contracts that inherit this contract cannot call this function.
        function privateFunc() private pure returns (string memory) {
            return "private function called";
        }

        function testPrivateFunc() public pure returns (string memory) {
            return privateFunc();
        }

        // Internal function can be called
        // - inside this contract
        // - inside contracts that inherit this contract
        function internalFunc() internal pure returns (string memory) {
            return "internal function called";
        }

        function testInternalFunc() public pure virtual returns (string memory) {
            return internalFunc();
        }

        // Public functions can be called
        // - inside this contract
        // - inside contracts that inherit this contract
        // - by other contracts and accounts
        function publicFunc() public pure returns (string memory) {
            return "public function called";
        }

        // External functions can only be called
        // - by other contracts and accounts
        function externalFunc() external pure returns (string memory) {
            return "external function called";
        }

        // This function will not compile since we're trying to call
        // an external function here.
        // function testExternalFunc() public pure returns (string memory) {
        //     return externalFunc();
        // }

        // State variables
        string private privateVar = "my private variable";
        string internal internalVar = "my internal variable";
        string public publicVar = "my public variable";
        // State variables cannot be external so this code won't compile.
        // string external externalVar = "my external variable";
    }

    contract Child is Base {
        // Inherited contracts do not have access to private functions
        // and state variables.
        // function testPrivateFunc() public pure returns (string memory) {
        //     return privateFunc();
        // }

        // Internal function call be called inside child contracts.
        function testInternalFunc() public pure override returns (string memory) {
            return internalFunc();
        }
    }

  ```

### è§†å›¾View-and-Pure-Functions

  Getterç±»å‹çš„å‡½æ•°å¯ä»¥å£°æ˜ä¸º`view`æˆ–`pure`ã€‚`view`å£°æ˜ä¸ä¼šæ›´æ”¹ä»»ä½•çŠ¶æ€ã€‚`pure`å£°æ˜ä¸ä¼šæ›´æ”¹æˆ–è¯»å–çŠ¶æ€å˜é‡ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract ViewAndPure {
        uint public x = 1;

        // Promise not to modify the state.
        function addToX(uint y) public view returns (uint) {
            return x + y;
        }

        // Promise not to modify or read from the state.
        function add(uint i, uint j) public pure returns (uint) {
            return i + j;
        }
    }

  ```


### å¯æ”¯ä»˜Payable

  è¢«`payable`ä¿®é¥°çš„å‡½æ•°æˆ–åœ°å€ï¼Œå¯ä»¥åœ¨åˆçº¦é‡Œæ¥æ”¶eth

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Payable {
        // Payable address can receive Ether
        address payable public owner;

        // Payable constructor can receive Ether
        constructor() payable {
            owner = payable(msg.sender);
        }

        // Function to deposit Ether into this contract.
        // Call this function along with some Ether.
        // The balance of this contract will be automatically updated.
        function deposit() public payable {}

        // Call this function along with some Ether.
        // The function will throw an error since this function is not payable.
        function notPayable() public {}

        // Function to withdraw all Ether from this contract.
        function withdraw() public {
            // get the amount of Ether stored in this contract
            uint amount = address(this).balance;

            // send all Ether to owner
            // Owner can receive Ether since the address of owner is payable
            (bool success, ) = owner.call{value: amount}("");
            require(success, "Failed to send Ether");
        }

        // Function to transfer Ether from this contract to address from input
        function transfer(address payable _to, uint _amount) public {
            // Note that "to" is declared as payable
            (bool success, ) = _to.call{value: _amount}("");
            require(success, "Failed to send Ether");
        }
    }

  ```

### å‡½æ•°ä¿®é¥°Function-Modifier

  modifierå¯åœ¨å‡½æ•°è¿è¡Œå‰æˆ–è€…è¿è¡Œä¹‹åæ‰§è¡Œ
  ä¸»è¦åŠŸèƒ½ï¼š
  - æƒé™è®¿é—®
  - éªŒè¯è¾“å…¥
  - é˜²æ­¢é‡å…¥æ”»å‡»


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract FunctionModifier {
        // We will use these variables to demonstrate how to use
        // modifiers.
        address public owner;
        uint public x = 10;
        bool public locked;

        constructor() {
            // Set the transaction sender as the owner of the contract.
            owner = msg.sender;
        }

        // Modifier to check that the caller is the owner of
        // the contract.
        modifier onlyOwner() {
            require(msg.sender == owner, "Not owner");
            // Underscore is a special character only used inside
            // a function modifier and it tells Solidity to
            // execute the rest of the code.
            _;
        }

        // Modifiers can take inputs. This modifier checks that the
        // address passed in is not the zero address.
        modifier validAddress(address _addr) {
            require(_addr != address(0), "Not valid address");
            _;
        }

        function changeOwner(address _newOwner) public onlyOwner validAddress(_newOwner) {
            owner = _newOwner;
        }

        // Modifiers can be called before and / or after a function.
        // This modifier prevents a function from being called while
        // it is still executing.
        modifier noReentrancy() {
            require(!locked, "No reentrancy");

            locked = true;
            _;
            locked = false;
        }

        function decrement(uint i) public noReentrancy {
            x -= i;

            if (i > 1) {
                decrement(i - 1);
            }
        }
    }

  ```

### é”™è¯¯åŠå¼‚å¸¸Error

error å°†åœ¨äº¤æ˜“æœŸé—´æ’¤æ¶ˆå¯¹çŠ¶æ€çš„æ‰€æœ‰æ›´æ”¹ã€‚
å¯ä»¥é€šè¿‡`require`ã€`revert` æˆ–è€…`assert`
- `require`ç”¨äºæ‰§è¡Œå‰éªŒè¯shu'ru'deã€‚
- `revert`ä¸`require`ç›¸ä¼¼ã€‚æœ‰å…³è¯¦ç»†ä¿¡æ¯ï¼Œè¯·å‚è§ä¸‹é¢çš„ä»£ç ã€‚
- `assert`æ–­è¨€ç”¨äºæ£€æŸ¥æ°¸è¿œä¸åº”è¯¥æ˜¯é”™è¯¯çš„ä»£ç ã€‚æ–­è¨€å¤±è´¥å¯èƒ½æ„å‘³ç€æœ‰ä¸€ä¸ªé”™è¯¯ã€‚
  
ä½¿ç”¨è‡ªå®šä¹‰é”™è¯¯èŠ‚çœgasã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Error {
        function testRequire(uint _i) public pure {
            // Require should be used to validate conditions such as:
            // - inputs
            // - conditions before execution
            // - return values from calls to other functions
            require(_i > 10, "Input must be greater than 10");
        }

        function testRevert(uint _i) public pure {
            // Revert is useful when the condition to check is complex.
            // This code does the exact same thing as the example above
            if (_i <= 10) {
                revert("Input must be greater than 10");
            }
        }

        uint public num;

        function testAssert() public view {
            // Assert should only be used to test for internal errors,
            // and to check invariants.

            // Here we assert that num is always equal to 0
            // since it is impossible to update the value of num
            assert(num == 0);
        }

        // custom error
        error InsufficientBalance(uint balance, uint withdrawAmount);

        function testCustomError(uint _withdrawAmount) public view {
            uint bal = address(this).balance;
            if (bal < _withdrawAmount) {
                revert InsufficientBalance({balance: bal, withdrawAmount: _withdrawAmount});
            }
        }
    }


  ```
  å…¶ä»–ç¤ºä¾‹

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Account {
        uint public balance;
        uint public constant MAX_UINT = 2**256 - 1;

        function deposit(uint _amount) public {
            uint oldBalance = balance;
            uint newBalance = balance + _amount;

            // balance + _amount does not overflow if balance + _amount >= balance
            require(newBalance >= oldBalance, "Overflow");

            balance = newBalance;

            assert(balance >= oldBalance);
        }

        function withdraw(uint _amount) public {
            uint oldBalance = balance;

            // balance - _amount does not underflow if balance >= _amount
            require(balance >= _amount, "Underflow");

            if (balance < _amount) {
                revert("Underflow");
            }

            balance -= _amount;

            assert(balance <= oldBalance);
        }
    }

  ```


### äº‹ä»¶Events

  äº‹ä»¶ä¸»è¦ç”¨äºè®°å½•åœ¨åŒºå—é“¾ä¸Š
  - ç›‘å¬äº‹ä»¶ï¼Œå¹¶æ›´æ–°ç”¨æˆ·çš„ä¸€äº›åŠŸèƒ½ï¼Œå¦‚ç•Œé¢ã€çŠ¶æ€ç­‰
  - ä¸€ä¸ªä¾¿å®œçš„å­˜å‚¨æ–¹å¼


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Event {
        // Event declaration
        // Up to 3 parameters can be indexed.
        // Indexed parameters helps you filter the logs by the indexed parameter
        event Log(address indexed sender, string message);
        event AnotherLog();

        function test() public {
            emit Log(msg.sender, "Hello World!");
            emit Log(msg.sender, "Hello EVM!");
            emit AnotherLog();
        }
    }

  ```

### æ„é€ å‡½æ•°Constructor

  æ„é€ å‡½æ•°æ˜¯å¯é€‰æ‹©çš„ä¸€ä¸ªå‡½æ•°ï¼Œå¯ä»¥ä¸å†™ï¼Œæ­¤å‡½æ•°ä¸»è¦åœ¨åˆçº¦åˆ›å»ºçš„æ—¶å€™æ‰§è¡Œ

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    // Base contract X
    contract X {
        string public name;

        constructor(string memory _name) {
            name = _name;
        }
    }

    // Base contract Y
    contract Y {
        string public text;

        constructor(string memory _text) {
            text = _text;
        }
    }

    // There are 2 ways to initialize parent contract with parameters.

    // Pass the parameters here in the inheritance list.
    contract B is X("Input to X"), Y("Input to Y") {

    }

    contract C is X, Y {
        // Pass the parameters here in the constructor,
        // similar to function modifiers.
        constructor(string memory _name, string memory _text) X(_name) Y(_text) {}
    }

    // Parent constructors are always called in the order of inheritance
    // regardless of the order of parent contracts listed in the
    // constructor of the child contract.

    // Order of constructors called:
    // 1. X
    // 2. Y
    // 3. D
    contract D is X, Y {
        constructor() X("X was called") Y("Y was called") {}
    }

    // Order of constructors called:
    // 1. X
    // 2. Y
    // 3. E
    contract E is X, Y {
        constructor() Y("Y was called") X("X was called") {}
    }

  ```

### ç»§æ‰¿Inheritance

  å¾ˆå¤šå˜æˆè¯­è¨€éƒ½ä¼šæœ‰æ­¤è¯­æ³•ï¼ŒSolidityæ”¯æŒå¤šç»§æ‰¿ï¼Œä½¿ç”¨`is`å…³é”®å­—ï¼Œ
  - å¦‚æœæƒ³è¦è¢«å­åˆçº¦é‡å†™ï¼Œçˆ¶åˆçº¦çš„å‡½æ•°å°±å¿…é¡»å«ä¸Š`virtual`çš„å£°æ˜
  - å­åˆçº¦å¦‚æœéœ€è¦é‡å†™çˆ¶ç±»çš„åˆçº¦å‡½æ•°ï¼Œå¿…é¡»åŠ ä¸Š`override`
  - ç»§æ‰¿çš„é¡ºåºå¾ˆé‡è¦
  - é€‚å½“çš„ï¼Œå¯ä»¥æ•´ç†å‡ºåˆçº¦çš„ç»§æ‰¿å…³ç³»ï¼Œé˜²æ­¢å‡ºç°é”™ä¹±é—®é¢˜


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    /* Graph of inheritance
        A
      / \
      B   C
    / \ /
    F  D,E

    */

    contract A {
        function foo() public pure virtual returns (string memory) {
            return "A";
        }
    }

    // Contracts inherit other contracts by using the keyword 'is'.
    contract B is A {
        // Override A.foo()
        function foo() public pure virtual override returns (string memory) {
            return "B";
        }
    }

    contract C is A {
        // Override A.foo()
        function foo() public pure virtual override returns (string memory) {
            return "C";
        }
    }

    // Contracts can inherit from multiple parent contracts.
    // When a function is called that is defined multiple times in
    // different contracts, parent contracts are searched from
    // right to left, and in depth-first manner.

    contract D is B, C {
        // D.foo() returns "C"
        // since C is the right most parent contract with function foo()
        function foo() public pure override(B, C) returns (string memory) {
            return super.foo();
        }
    }

    contract E is C, B {
        // E.foo() returns "B"
        // since B is the right most parent contract with function foo()
        function foo() public pure override(C, B) returns (string memory) {
            return super.foo();
        }
    }

    // Inheritance must be ordered from â€œmost base-likeâ€ to â€œmost derivedâ€.
    // Swapping the order of A and B will throw a compilation error.
    contract F is A, B {
        function foo() public pure override(A, B) returns (string memory) {
            return super.foo();
        }
    }

  ```


### ç»§æ‰¿çš„çŠ¶æ€å˜é‡Shadowing-Inherited-State-Variables

  çŠ¶æ€å˜é‡ï¼Œä¸èƒ½åœ¨å­åˆçº¦é‡æ–°å£°æ˜æ¥é‡å†™

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract A {
        string public name = "Contract A";

        function getName() public view returns (string memory) {
            return name;
        }
    }

    // Shadowing is disallowed in Solidity 0.6
    // This will not compile
    // contract B is A {
    //     string public name = "Contract B";
    // }

    contract C is A {
        // This is the correct way to override inherited state variables.
        constructor() {
            name = "Contract C";
        }

        // C.getName returns "Contract C"
    }

  ```


### è°ƒç”¨çˆ¶ç±»å‡½æ•°Calling-Parent-Contracts

  çˆ¶åˆçº¦å¯ä»¥ç›´æ¥è°ƒç”¨ï¼Œä¹Ÿå¯ä»¥ä½¿ç”¨å…³é”®å­—`super`æ¥è°ƒç”¨ã€‚
  é€šè¿‡ä½¿ç”¨å…³é”®å­—superï¼Œå°†è°ƒç”¨æ‰€æœ‰ç›´æ¥çˆ¶åˆçº¦ä¸­çš„å‡½æ•°ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    /* Inheritance tree
      A
    /  \
    B   C
    \ /
      D
    */

    contract A {
        // This is called an event. You can emit events from your function
        // and they are logged into the transaction log.
        // In our case, this will be useful for tracing function calls.
        event Log(string message);

        function foo() public virtual {
            emit Log("A.foo called");
        }

        function bar() public virtual {
            emit Log("A.bar called");
        }
    }

    contract B is A {
        function foo() public virtual override {
            emit Log("B.foo called");
            A.foo();
        }

        function bar() public virtual override {
            emit Log("B.bar called");
            super.bar();
        }
    }

    contract C is A {
        function foo() public virtual override {
            emit Log("C.foo called");
            A.foo();
        }

        function bar() public virtual override {
            emit Log("C.bar called");
            super.bar();
        }
    }

    contract D is B, C {
        // Try:
        // - Call D.foo and check the transaction logs.
        //   Although D inherits A, B and C, it only called C and then A.
        // - Call D.bar and check the transaction logs
        //   D called C, then B, and finally A.
        //   Although super was called twice (by B and C) it only called A once.

        function foo() public override(B, C) {
            super.foo();
        }

        function bar() public override(B, C) {
            super.bar();
        }
    }

  ```


### æ¥å£Interface

  æ‚¨å¯ä»¥é€šè¿‡å£°æ˜ä¸€ä¸ªInterfaceä¸å…¶ä»–åˆçº¦è¿›è¡Œäº¤äº’ã€‚
  Interface
  - æ²¡æœ‰ä»»ä½•è¯¦ç»†çš„é€»è¾‘åŠŸèƒ½
  - å¯ä»¥ç»§æ‰¿å…¶ä»–çš„interfase
  - æ‰€æœ‰çš„å¯è§æ€§å£°æ˜å¿…é¡»æ˜¯external
  - æ²¡æœ‰constructorå‡½æ•°
  - ä¸èƒ½å£°æ˜çŠ¶æ€å˜é‡


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Counter {
        uint public count;

        function increment() external {
            count += 1;
        }
    }

    interface ICounter {
        function count() external view returns (uint);

        function increment() external;
    }

    contract MyContract {
        function incrementCounter(address _counter) external {
            ICounter(_counter).increment();
        }

        function getCount(address _counter) external view returns (uint) {
            return ICounter(_counter).count();
        }
    }

    // Uniswap example
    interface UniswapV2Factory {
        function getPair(address tokenA, address tokenB)
            external
            view
            returns (address pair);
    }

    interface UniswapV2Pair {
        function getReserves()
            external
            view
            returns (
                uint112 reserve0,
                uint112 reserve1,
                uint32 blockTimestampLast
            );
    }

    contract UniswapExample {
        address private factory = 0x5C69bEe701ef814a2B6a3EDD4B1652CB9cc5aA6f;
        address private dai = 0x6B175474E89094C44Da98b954EedeAC495271d0F;
        address private weth = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;

        function getTokenReserves() external view returns (uint, uint) {
            address pair = UniswapV2Factory(factory).getPair(dai, weth);
            (uint reserve0, uint reserve1, ) = UniswapV2Pair(pair).getReserves();
            return (reserve0, reserve1);
        }
    }

  ```


### å‘é€eth-Sendingâ€“Ether-Transfer-Send-and-Call

å¦‚ä½•å‘é€eth
å‘é€ethåˆ°å…¶ä»–åˆçº¦ï¼š
  - transfer (2300 gas, throws error)
  - send (2300 gas, returns bool)
  - call (forward all gas or set gas, returns bool)

å¦‚ä½•æ¥æ”¶eth
å¦‚æœä¸€ä¸ªåˆçº¦å¯ä»¥ç›´æ¥æ¥æ”¶ethçš„è½¬è´¦éœ€è¦å†™å¦‚ä¸‹çš„å‡½æ•°ï¼š
  - receive() external payable
  - fallback() external payable

å»ºè®®ä½¿ç”¨

- `call` åœ¨2019å¹´12æœˆä¹‹åï¼Œå»ºè®®ä½¿ç”¨ä¸é‡å…¥ä¿æŠ¤ç›¸ç»“åˆçš„å‡½æ•°ã€‚

é˜²æ­¢é‡å…¥
- å†è°ƒç”¨å…¶ä»–åˆçº¦ä¹‹å‰ï¼Œå¤„ç†å¥½æ‰€æœ‰çš„çŠ¶æ€æ”¹å˜
- ä½¿ç”¨modifierå‡½æ•°é˜²æ­¢


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract ReceiveEther {
        /*
        Which function is called, fallback() or receive()?

              send Ether
                  |
            msg.data is empty?
                  / \
                yes  no
                /     \
    receive() exists?  fallback()
            /   \
            yes   no
            /      \
        receive()   fallback()
        */

        // Function to receive Ether. msg.data must be empty
        receive() external payable {}

        // Fallback function is called when msg.data is not empty
        fallback() external payable {}

        function getBalance() public view returns (uint) {
            return address(this).balance;
        }
    }

    contract SendEther {
        function sendViaTransfer(address payable _to) public payable {
            // This function is no longer recommended for sending Ether.
            _to.transfer(msg.value);
        }

        function sendViaSend(address payable _to) public payable {
            // Send returns a boolean value indicating success or failure.
            // This function is not recommended for sending Ether.
            bool sent = _to.send(msg.value);
            require(sent, "Failed to send Ether");
        }

        function sendViaCall(address payable _to) public payable {
            // Call returns a boolean value indicating success or failure.
            // This is the current recommended method to use.
            (bool sent, bytes memory data) = _to.call{value: msg.value}("");
            require(sent, "Failed to send Ether");
        }
    }

  ```

### å›è°ƒFallback

`fallback`æ˜¯ä¸€ä¸ªä¸æ¥å—ä»»ä½•å‚æ•°ä¹Ÿä¸è¿”å›ä»»ä½•å†…å®¹çš„å‡½æ•°ã€‚

ä¼šæ‰§è¡Œçš„éƒ¨åˆ†
  - ä¸€ä¸ªä¸å­˜åœ¨çš„å‡½æ•°è¢«è°ƒç”¨
  - ethè¢«ç›´æ¥å‘é€åˆ°ä¸€ä¸ªåˆçº¦ï¼Œä½†æ˜¯`receive()`ä¸å­˜åœ¨æˆ–`msg.data`æ•°æ®ä¸æ˜¯ç©ºçš„
å½“é€šè¿‡`transfer`æˆ–`send`è°ƒç”¨æ—¶ï¼Œ`fallback` ä¸Šé™ä¸º2300 gas limitã€‚


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Fallback {
        event Log(uint gas);

        // Fallback function must be declared as external.
        fallback() external payable {
            // send / transfer (forwards 2300 gas to this fallback function)
            // call (forwards all of the gas)
            emit Log(gasleft());
        }

        // Helper function to check the balance of this contract
        function getBalance() public view returns (uint) {
            return address(this).balance;
        }
    }

    contract SendToFallback {
        function transferToFallback(address payable _to) public payable {
            _to.transfer(msg.value);
        }

        function callFallback(address payable _to) public payable {
            (bool sent, ) = _to.call{value: msg.value}("");
            require(sent, "Failed to send Ether");
        }
    }

  ```

### è®¿é—®Call

  `call`æ˜¯ä¸å…¶ä»–åˆçº¦äº¤äº’çš„è¾ƒä¸ºåº•å±‚çš„å‡½æ•°ã€‚
  å½“æ‚¨é€šè¿‡è°ƒç”¨`fallback`å‡½æ•°æ¥å‘é€ethæ—¶ï¼Œæ¨èä½¿ç”¨è¿™ç§å‡½æ•°ã€‚
  ä½†æ˜¯ï¼Œè¿™ä¸æ˜¯è°ƒç”¨ç°æœ‰å‡½æ•°çš„æ¨èæ–¹å¼ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Receiver {
        event Received(address caller, uint amount, string message);

        fallback() external payable {
            emit Received(msg.sender, msg.value, "Fallback was called");
        }

        function foo(string memory _message, uint _x) public payable returns (uint) {
            emit Received(msg.sender, msg.value, _message);

            return _x + 1;
        }
    }

    contract Caller {
        event Response(bool success, bytes data);

        // Let's imagine that contract B does not have the source code for
        // contract A, but we do know the address of A and the function to call.
        function testCallFoo(address payable _addr) public payable {
            // You can send ether and specify a custom gas amount
            (bool success, bytes memory data) = _addr.call{value: msg.value, gas: 5000}(
                abi.encodeWithSignature("foo(string,uint256)", "call foo", 123)
            );

            emit Response(success, data);
        }

        // Calling a function that does not exist triggers the fallback function.
        function testCallDoesNotExist(address _addr) public {
            (bool success, bytes memory data) = _addr.call(
                abi.encodeWithSignature("doesNotExist()")
            );

            emit Response(success, data);
        }
    }

  ```

### å§”æ‰˜è®¿é—®Delegatecall

  ä¸`call`ç±»ä¼¼ï¼Œä¹Ÿæ˜¯åº•å±‚çš„å‡½æ•°
  å½“åˆçº¦Aæ‰§è¡Œå§”æ‰˜è°ƒç”¨ç»™åˆçº¦Bæ—¶ï¼Œå°±ä¼šæ‰§è¡Œåˆçº¦Bçš„ä»£ç 
  å¹¶ä¸”ä½¿ç”¨Açš„å­˜å‚¨ã€`msg.sender`å’Œ`msg.value`

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    // NOTE: Deploy this contract first
    contract B {
        // NOTE: storage layout must be the same as contract A
        uint public num;
        address public sender;
        uint public value;

        function setVars(uint _num) public payable {
            num = _num;
            sender = msg.sender;
            value = msg.value;
        }
    }

    contract A {
        uint public num;
        address public sender;
        uint public value;

        function setVars(address _contract, uint _num) public payable {
            // A's storage is set, B is not modified.
            (bool success, bytes memory data) = _contract.delegatecall(
                abi.encodeWithSignature("setVars(uint256)", _num)
            );
        }
    }

  ```


### é€‰æ‹©å™¨Function-Selector

  è°ƒç”¨å‡½æ•°æ—¶ï¼Œ`calldata` çš„å‰4ä¸ªå­—èŠ‚æŒ‡å®šè¦è°ƒç”¨å“ªä¸ªå‡½æ•°ã€‚
  è¿™4ä¸ªå­—èŠ‚è¢«ç§°ä¸ºå‡½æ•°é€‰æ‹©å™¨ã€‚
  ä»¥ä¸‹é¢çš„ä»£ç ä¸ºä¾‹ã€‚å®ƒä½¿ç”¨è°ƒç”¨åœ¨åœ°å€addrå¤„æ‰§è¡Œåˆçº¦çš„è½¬ç§»ã€‚

  ```js
  addr.call(abi.encodeWithSignature("transfer(address,uint256)", 0xaaaaa, 123))
  ```

  abi.encodeWithSignature(....)è¿”å›çš„å‰4ä¸ªå­—èŠ‚æ˜¯å‡½æ•°é€‰æ‹©å™¨ã€‚
  å¦‚æœé¢„å…ˆè®¡ç®—å¹¶åœ¨ä»£ç ä¸­å†…è”å‡½æ•°é€‰æ‹©å™¨ï¼Œä¹Ÿè®¸å¯ä»¥èŠ‚çœå°‘é‡çš„gas
  ä¸‹é¢æ˜¯å‡½æ•°é€‰æ‹©å™¨çš„è®¡ç®—å‡½æ•°ã€‚
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract FunctionSelector {
        /*
        "transfer(address,uint256)"
        0xa9059cbb
        "transferFrom(address,address,uint256)"
        0x23b872dd
        */
        function getSelector(string calldata _func) external pure returns (bytes4) {
            return bytes4(keccak256(bytes(_func)));
        }
    }

  ```


### è°ƒç”¨å…¶ä»–åˆçº¦Calling-Other-Contract

  Contractå¯ä»¥é€šè¿‡ä¸¤ç§æ–¹å¼è°ƒç”¨å…¶ä»–Contractã€‚
  æœ€ç®€å•çš„å‡½æ•°æ˜¯ç›´æ¥è°ƒç”¨å®ƒï¼Œæ¯”å¦‚A.foo(x, y, z)ã€‚
  è°ƒç”¨å…¶ä»–åˆçº¦çš„å¦ä¸€ç§å‡½æ•°æ˜¯ä½¿ç”¨åº•å±‚è°ƒç”¨ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Callee {
        uint public x;
        uint public value;

        function setX(uint _x) public returns (uint) {
            x = _x;
            return x;
        }

        function setXandSendEther(uint _x) public payable returns (uint, uint) {
            x = _x;
            value = msg.value;

            return (x, value);
        }
    }

    contract Caller {
        function setX(Callee _callee, uint _x) public {
            uint x = _callee.setX(_x);
        }

        function setXFromAddress(address _addr, uint _x) public {
            Callee callee = Callee(_addr);
            callee.setX(_x);
        }

        function setXandSendEther(Callee _callee, uint _x) public payable {
            (uint x, uint value) = _callee.setXandSendEther{value: msg.value}(_x);
        }
    }

  ```


### ä½¿ç”¨åˆçº¦åˆ›å»ºåˆçº¦Creates-other-Contracts

  å…¶ä»–åˆçº¦å¯ä»¥ä½¿ç”¨newå…³é”®å­—åˆ›å»ºåˆçº¦ã€‚ä»0.8.0å¼€å§‹ï¼Œnewå…³é”®å­—é€šè¿‡æŒ‡å®šsalté€‰é¡¹æ”¯æŒcreate2ç‰¹æ€§ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract Car {
        address public owner;
        string public model;
        address public carAddr;

        constructor(address _owner, string memory _model) payable {
            owner = _owner;
            model = _model;
            carAddr = address(this);
        }
    }

    contract CarFactory {
        Car[] public cars;

        function create(address _owner, string memory _model) public {
            Car car = new Car(_owner, _model);
            cars.push(car);
        }

        function createAndSendEther(address _owner, string memory _model) public payable {
            Car car = (new Car){value: msg.value}(_owner, _model);
            cars.push(car);
        }

        function create2(
            address _owner,
            string memory _model,
            bytes32 _salt
        ) public {
            Car car = (new Car){salt: _salt}(_owner, _model);
            cars.push(car);
        }

        function create2AndSendEther(
            address _owner,
            string memory _model,
            bytes32 _salt
        ) public payable {
            Car car = (new Car){value: msg.value, salt: _salt}(_owner, _model);
            cars.push(car);
        }

        function getCar(uint _index)
            public
            view
            returns (
                address owner,
                string memory model,
                address carAddr,
                uint balance
            )
        {
            Car car = cars[_index];

            return (car.owner(), car.model(), car.carAddr(), address(car).balance);
        }
    }

  ```


### å¼‚å¸¸æ•æ‰Try-Catch

   `try / catch`åªèƒ½ä»`external`å‡½æ•°å’ŒåˆåŒåˆ›å»ºä¸­æ•è·é”™è¯¯ã€‚


  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    // External contract used for try / catch examples
    contract Foo {
        address public owner;

        constructor(address _owner) {
            require(_owner != address(0), "invalid address");
            assert(_owner != 0x0000000000000000000000000000000000000001);
            owner = _owner;
        }

        function myFunc(uint x) public pure returns (string memory) {
            require(x != 0, "require failed");
            return "my func was called";
        }
    }

    contract Bar {
        event Log(string message);
        event LogBytes(bytes data);

        Foo public foo;

        constructor() {
            // This Foo contract is used for example of try catch with external call
            foo = new Foo(msg.sender);
        }

        // Example of try / catch with external call
        // tryCatchExternalCall(0) => Log("external call failed")
        // tryCatchExternalCall(1) => Log("my func was called")
        function tryCatchExternalCall(uint _i) public {
            try foo.myFunc(_i) returns (string memory result) {
                emit Log(result);
            } catch {
                emit Log("external call failed");
            }
        }

        // Example of try / catch with contract creation
        // tryCatchNewContract(0x0000000000000000000000000000000000000000) => Log("invalid address")
        // tryCatchNewContract(0x0000000000000000000000000000000000000001) => LogBytes("")
        // tryCatchNewContract(0x0000000000000000000000000000000000000002) => Log("Foo created")
        function tryCatchNewContract(address _owner) public {
            try new Foo(_owner) returns (Foo foo) {
                // you can use variable foo here
                emit Log("Foo created");
            } catch Error(string memory reason) {
                // catch failing revert() and require()
                emit Log(reason);
            } catch (bytes memory reason) {
                // catch failing assert()
                emit LogBytes(reason);
            }
        }
    }

  ```

### å¯¼å…¥ Import

  Solidityæ”¯æŒå¯¼å…¥æœ¬åœ°å’Œå¤–éƒ¨æ–‡ä»¶ã€‚

  æœ¬åœ°
  ç›®å½•ç»“æ„å¯ä»¥å¦‚ä¸‹ï¼š
  ```js
  â”œâ”€â”€ Import.sol
  â””â”€â”€ Foo.sol
  ```
#### Foo.sol
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    struct Point {
        uint x;
        uint y;
    }

    error Unauthorized(address caller);

    function add(uint x, uint y) pure returns (uint) {
        return x + y;
    }

    contract Foo {
        string public name = "Foo";
    }

  ```
#### Import.sol
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    // import Foo.sol from current directory
    import "./Foo.sol";

    // import {symbol1 as alias, symbol2} from "filename";
    import {Unauthorized, add as func, Point} from "./Foo.sol";

    contract Import {
        // Initialize Foo.sol
        Foo public foo = new Foo();

        // Test Foo.sol by getting it's name.
        function getFooName() public view returns (string memory) {
            return foo.name();
        }
    }

  ```
#### å¤–éƒ¨èµ„æº
  ä½ ä¹Ÿå¯ä»¥é€šè¿‡ç®€å•åœ°å¤åˆ¶urlä»GitHubå¯¼å…¥
  ```js
  import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/release-v4.0/contracts/utils/math/SafeMath.sol";

  // Example import ECDSA.sol from openzeppelin-contract repo, release-v4.5 branch
  // https://github.com/OpenZeppelin/openzeppelin-contracts/blob/release-v4.5/contracts/utils/cryptography/ECDSA.sol
  import "https://github.com/OpenZeppelin/openzeppelin-contracts/blob/release-v4.5/contracts/utils/cryptography/ECDSA.sol";

  ```


### åº“Library

  åº“ç±»ä¼¼äºåˆçº¦ï¼Œä½†ä¸èƒ½å£°æ˜ä»»ä½•çŠ¶æ€å˜é‡ï¼Œä¹Ÿä¸èƒ½å‘é€ä»¥å¤ªã€‚
  å¦‚æœæ‰€æœ‰åº“å‡½æ•°éƒ½æ˜¯å†…éƒ¨çš„ï¼Œåˆ™åº“åµŒå…¥åˆ°åˆçº¦ä¸­ï¼Œå¦åˆ™ï¼Œå¿…é¡»å…ˆéƒ¨ç½²åº“ï¼Œç„¶ååœ¨éƒ¨ç½²å¥‘çº¦ä¹‹å‰é“¾æ¥åº“ã€‚

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    library SafeMath {
        function add(uint x, uint y) internal pure returns (uint) {
            uint z = x + y;
            require(z >= x, "uint overflow");

            return z;
        }
    }

    library Math {
        function sqrt(uint y) internal pure returns (uint z) {
            if (y > 3) {
                z = y;
                uint x = y / 2 + 1;
                while (x < z) {
                    z = x;
                    x = (y / x + x) / 2;
                }
            } else if (y != 0) {
                z = 1;
            }
            // else z = 0 (default value)
        }
    }

    contract TestSafeMath {
        using SafeMath for uint;

        uint public MAX_UINT = 2**256 - 1;

        function testAdd(uint x, uint y) public pure returns (uint) {
            return x.add(y);
        }

        function testSquareRoot(uint x) public pure returns (uint) {
            return Math.sqrt(x);
        }
    }

    // Array function to delete element at index and re-organize the array
    // so that their are no gaps between the elements.
    library Array {
        function remove(uint[] storage arr, uint index) public {
            // Move the last element into the place to delete
            require(arr.length > 0, "Can't remove from empty array");
            arr[index] = arr[arr.length - 1];
            arr.pop();
        }
    }

    contract TestArray {
        using Array for uint[];

        uint[] public arr;

        function testArrayRemove() public {
            for (uint i = 0; i < 3; i++) {
                arr.push(i);
            }

            arr.remove(1);

            assert(arr.length == 2);
            assert(arr[0] == 0);
            assert(arr[1] == 2);
        }
    }

  ```
### ABI-Decode

  abi.encode encodes data into bytes.
  abi.decode decodes bytes back into data.

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract AbiDecode {
        struct MyStruct {
            string name;
            uint[2] nums;
        }

        function encode(
            uint x,
            address addr,
            uint[] calldata arr,
            MyStruct calldata myStruct
        ) external pure returns (bytes memory) {
            return abi.encode(x, addr, arr, myStruct);
        }

        function decode(bytes calldata data)
            external
            pure
            returns (
                uint x,
                address addr,
                uint[] memory arr,
                MyStruct memory myStruct
            )
        {
            // (uint x, address addr, uint[] memory arr, MyStruct myStruct) = ...
            (x, addr, arr, myStruct) = abi.decode(data, (uint, address, uint[], MyStruct));
        }
    }

  ```

### Hashing-with-Keccak256

  keccak256è®¡ç®—è¾“å…¥çš„kecak -256å“ˆå¸Œå€¼ã€‚

  ä½¿ç”¨åœºæ™¯
  - æ ¹æ®è¾“å…¥å†…å®¹åˆ›å»ºå”¯ä¸€id
  - Commit-Revealæ–¹æ¡ˆ
  - ç¼©å°è¾“å…¥çš„æ•°æ®ï¼Œç­¾ååçš„ç»“æœè¾“å…¥ç»“æœéƒ½è¾ƒå°ï¼Œå¯ä»¥ä½œä¸ºè¾“å…¥ä½¿ç”¨

  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    contract HashFunction {
        function hash(
            string memory _text,
            uint _num,
            address _addr
        ) public pure returns (bytes32) {
            return keccak256(abi.encodePacked(_text, _num, _addr));
        }

        // Example of hash collision
        // Hash collision can occur when you pass more than one dynamic data type
        // to abi.encodePacked. In such case, you should use abi.encode instead.
        function collision(string memory _text, string memory _anotherText)
            public
            pure
            returns (bytes32)
        {
            // encodePacked(AAA, BBB) -> AAABBB
            // encodePacked(AA, ABBB) -> AAABBB
            return keccak256(abi.encodePacked(_text, _anotherText));
        }
    }

    contract GuessTheMagicWord {
        bytes32 public answer =
            0x60298f78cc0b47170ba79c10aa3851d7648bd96f2f8e46a19dbc777c36fb0c00;

        // Magic word is "Solidity"
        function guess(string memory _word) public view returns (bool) {
            return keccak256(abi.encodePacked(_word)) == answer;
        }
    }

  ```

### éªŒè¯ç­¾åVerifying-Signature

æ¶ˆæ¯å¯ä»¥åœ¨é“¾å¤–ç­¾åï¼Œç„¶ååœ¨é“¾ä¸Šä½¿ç”¨æ™ºèƒ½åˆçº¦è¿›è¡ŒéªŒè¯ã€‚

ethers.jsç¤ºä¾‹
  ```js
    const accounts = await ethers.getSigners(2)
    const VerifySignature = await ethers.getContractFactory("VerifySignature")
    const contract = await VerifySignature.deploy()
    await contract.deployed()

    // const PRIV_KEY = "0x..."
    // const signer = new ethers.Wallet(PRIV_KEY)
    const signer = accounts[0]
    const to = accounts[1].address
    const amount = 999
    const message = "Hello"
    const nonce = 123

    const hash = await contract.getMessageHash(to, amount, message, nonce)
    const sig = await signer.signMessage(ethers.utils.arrayify(hash))

    const ethHash = await contract.getEthSignedMessageHash(hash)

    console.log("signer          ", signer.address)
    console.log("recovered signer", await contract.recoverSigner(ethHash, sig))

    // Correct signature and message returns true
    expect(
      await contract.verify(signer.address, to, amount, message, nonce, sig)
    ).to.equal(true)

    // Incorrect message returns false
    expect(
      await contract.verify(signer.address, to, amount + 1, message, nonce, sig)
    ).to.equal(false)
  ```

  ```js
  // SPDX-License-Identifier: MIT
  pragma solidity ^0.8.13;

  /* Signature Verification

  How to Sign and Verify
  # Signing
  1. Create message to sign
  2. Hash the message
  3. Sign the hash (off chain, keep your private key secret)

  # Verify
  1. Recreate hash from the original message
  2. Recover signer from signature and hash
  3. Compare recovered signer to claimed signer
  */

  contract VerifySignature {
      /* 1. Unlock MetaMask account
      ethereum.enable()
      */

      /* 2. Get message hash to sign
      getMessageHash(
          0x14723A09ACff6D2A60DcdF7aA4AFf308FDDC160C,
          123,
          "coffee and donuts",
          1
      )

      hash = "0xcf36ac4f97dc10d91fc2cbb20d718e94a8cbfe0f82eaedc6a4aa38946fb797cd"
      */
      function getMessageHash(
          address _to,
          uint _amount,
          string memory _message,
          uint _nonce
      ) public pure returns (bytes32) {
          return keccak256(abi.encodePacked(_to, _amount, _message, _nonce));
      }

      /* 3. Sign message hash
      # using browser
      account = "copy paste account of signer here"
      ethereum.request({ method: "personal_sign", params: [account, hash]}).then(console.log)

      # using web3
      web3.personal.sign(hash, web3.eth.defaultAccount, console.log)

      Signature will be different for different accounts
      0x993dab3dd91f5c6dc28e17439be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063e8f3f781b
      */
      function getEthSignedMessageHash(bytes32 _messageHash)
          public
          pure
          returns (bytes32)
      {
          /*
          Signature is produced by signing a keccak256 hash with the following format:
          "\x19Ethereum Signed Message\n" + len(msg) + msg
          */
          return
              keccak256(
                  abi.encodePacked("\x19Ethereum Signed Message:\n32", _messageHash)
              );
      }

      /* 4. Verify signature
      signer = 0xB273216C05A8c0D4F0a4Dd0d7Bae1D2EfFE636dd
      to = 0x14723A09ACff6D2A60DcdF7aA4AFf308FDDC160C
      amount = 123
      message = "coffee and donuts"
      nonce = 1
      signature =
          0x993dab3dd91f5c6dc28e17439be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063e8f3f781b
      */
      function verify(
          address _signer,
          address _to,
          uint _amount,
          string memory _message,
          uint _nonce,
          bytes memory signature
      ) public pure returns (bool) {
          bytes32 messageHash = getMessageHash(_to, _amount, _message, _nonce);
          bytes32 ethSignedMessageHash = getEthSignedMessageHash(messageHash);

          return recoverSigner(ethSignedMessageHash, signature) == _signer;
      }

      function recoverSigner(bytes32 _ethSignedMessageHash, bytes memory _signature)
          public
          pure
          returns (address)
      {
          (bytes32 r, bytes32 s, uint8 v) = splitSignature(_signature);

          return ecrecover(_ethSignedMessageHash, v, r, s);
      }

      function splitSignature(bytes memory sig)
          public
          pure
          returns (
              bytes32 r,
              bytes32 s,
              uint8 v
          )
      {
          require(sig.length == 65, "invalid signature length");

          assembly {
              /*
              First 32 bytes stores the length of the signature

              add(sig, 32) = pointer of sig + 32
              effectively, skips first 32 bytes of signature

              mload(p) loads next 32 bytes starting at the memory address p into memory
              */

              // first 32 bytes, after the length prefix
              r := mload(add(sig, 32))
              // second 32 bytes
              s := mload(add(sig, 64))
              // final byte (first byte of the next 32 bytes)
              v := byte(0, mload(add(sig, 96)))
          }

          // implicitly return (r, s, v)
      }
  }

  ```

### gasä¼˜åŒ–Gas-Optimizations

  gasä¼˜åŒ–æ–¹æ¡ˆ
  - ç”¨`calldata`æ›¿æ¢`memory`
  - çŠ¶æ€å˜é‡åŠ è½½åˆ°å†…å­˜
  - ç”¨++iæ›¿æ¢å¾ªç¯i++
  - ç¼“å­˜æ•°ç»„å…ƒç´ 
  - å‡å°‘å¾ªç¯
  
  ```js
    // SPDX-License-Identifier: MIT
    pragma solidity ^0.8.13;

    // gas golf
    contract GasGolf {
        // start - 50908 gas
        // use calldata - 49163 gas
        // load state variables to memory - 48952 gas
        // short circuit - 48634 gas
        // loop increments - 48244 gas
        // cache array length - 48209 gas
        // load array elements to memory - 48047 gas
        // uncheck i overflow/underflow - 47309 gas

        uint public total;

        // start - not gas optimized
        // function sumIfEvenAndLessThan99(uint[] memory nums) external {
        //     for (uint i = 0; i < nums.length; i += 1) {
        //         bool isEven = nums[i] % 2 == 0;
        //         bool isLessThan99 = nums[i] < 99;
        //         if (isEven && isLessThan99) {
        //             total += nums[i];
        //         }
        //     }
        // }

        // gas optimized
        // [1, 2, 3, 4, 5, 100]
        function sumIfEvenAndLessThan99(uint[] calldata nums) external {
            uint _total = total;
            uint len = nums.length;

            for (uint i = 0; i < len; ) {
                uint num = nums[i];
                if (num % 2 == 0 && num < 99) {
                    _total += num;
                }
                unchecked {
                    ++i;
                }
            }

            total = _total;
        }
    }

  ```



